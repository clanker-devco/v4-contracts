# Clanker v4.0.0

Clanker is a token launchpad enabling entities to deploy tokens and earn a portion of the trading fees generated from those tokens. v4.0.0 introduces modularity into the Clanker factory contract, allowing for more flexible and customizable token deployments over time.

## Changes from v3.1.0

Clanker v4.0.0 introduces the following major changes from v3.1.0:

- **Uniswap v4 Hooks**: The Clanker factory is able to deploy configurable Uniswap v4 pools via hooks that implement our `IClankerHooks` interface. On v4.0.0 launch, we're including two hooks for users to choose between: a configurable static fee pool and a dynamic fee pool. Clanker also uses the v4 pools to enable automatic LP fee collection and a DEX-level Clanker Protocol Fee.
- **Extensions**: Extensions are contracts that can be used to add new functionality to the token deployment process. Specifically, they are able to request a portion of the token supply and a `msg.value` to be sent to them during the token deployment process. The portion of the token supply is reduced from the amount that would otherwise be used in the pool's initial liquidity positions. On v4.0.0 launch, available extensions include: a token vault (similar to v3.1.0), a devBuy mechanism (similar to v3.1.0), and a new airdrop mechanism. Up to 90% of the token supply can be allocated to extensions.
- **Multiple initial LP Positions**: Liquidity placement for v4 is now more flexible. Users can configure up to 7 different liquidity positions per token, enabling customized liquidity placement for different token profiles.
- **More reward recipients**: Deployers can now specify up to 7 different LP reward recipients per deployment. There is no longer a concept of a 'creator' or 'interface'; the reward recipients can be whoever the deployer chooses.
- **Easier unclaimed fee insights**: With the automatic LP fee collection enabled by the v4 pools, users are now able to view the amount of fees they have accrued but not yet claimed. User fees are stored in the `ClankerFeeLocker` with the amount of unclaimed fees easily viewable by the user.
- **Clanker Protocol Fee**: Clanker now has a protocol fee which is collected on all trades using the Clanker Hooks. This fee was previously collected by routing a portion of the LP fees to Clanker. The protocol fee is collected in the paired token and is always 20% of the LP fee.
- **MEV Modules**: Clanker v4.0.0 introduces the ability to use MEV modules during a pool's first few swaps. Currently, the only MEV module available is a 2-block delay module which makes the pool untradeable until two blocks have passed. This is to prevent people from searching for deployments in the same block of creation. We plan to build out chain-specific MEV modules in the future to run sniper auctions (see our [Auction Proposal](./AuctionProposal.md) for more details).

## Core Contracts

Clanker v4.0.0 consists of seven main contracts, five of which are implementations and two of which are interfaces:

1. **ClankerToken**: A super-chain compatible ERC20 contract.
2. **Clanker**: The factory contract responsible for token deployment, triggering pool creation, triggering liquidity placement via the LpLocker, and triggering extensions.
3. **ClankerFeeLocker**: A contract which allows users to view and claim their accumulated LP fees. This single contract accrues fees for all tokens deployed on Clanker v4.0.0.
4. **ClankerLpLockerMultiple**: The contract responsible for placing liquidity on the pool, collecting the LP fees from the pool, and dividing them between the LP reward recipients before transferring them to the `ClankerFeeLocker`.
5. **ClankerHook**: Clanker's base hook contract which facilitates the collection of the protocol fee, the automatic claiming of the LP fees, and the MEV module functionality. This is an abstract contract which is extended by our hook implementation contracts.
6. **IClankerExtensions**: An interface for Clanker extensions to implement to be compatible with Clanker's v4.0.0 factory.
7. **IClankerMevModule**: An interface for the MEV modules to implement to be compatible with Clanker's v4.0.0 factory.

## Modular Contracts
Clanker v4.0.0 also includes the following contracts which implement the interfaces and are ready for use on v4.0.0 launch:

_ClankerHooks_:
- **ClankerHookStaticFee**: An extension of the `ClankerHook` which enables users to set a static fee for both the deployed token and the paired token.
- **ClankerHookDynamicFee**: An extension of the `ClankerHook` which enables users to configure a dynamic fee that adjusts based on the amount of volatility in the pool.

_IClankerExtensions_:
- **ClankerVault**: An extension which allows deployers to vault a portion of the token supply with an optional linear vest option post unlock. This differs from the v3.1.0 vault in that there is no longer a minimum lockup time or a maximum supply limit.
- **ClankerAirdrop**: An extension which allows for a portion of the token supply to be allocated to airdrop recipients. Users of the extension must supply a merkle root whose leaves contain the airdrop recipient and the amount of tokens each recipient is entitled to. An optional lockup and linear vesting option is also available.
- **ClankerUniv4EthDevBuy**: An extension which allows for the deployer to perform an initial swap of the token using passed-in ETH. This provides the same functionality as the v3.1.0 `initialSwap`. Note: if the paired token for the pool is not WETH, only paired tokens which have a W/ETH<>PairedToken Uniswap v4 pool will be able to use this extension.

_IClankerMevModule_:
- **ClankerMevModule2BlockDelay**: A MEV module which makes the pool untradeable for the first 2 blocks after deployment. This prevents people from searching for deployments in the same block of creation and helps prepare snipers for auctions.

## Clanker Factory

### Token Deployment

Tokens are deployed through the `deployToken()` function:

```solidity
/**
* Configuration settings for token creation
*/

struct TokenConfig {
    address tokenAdmin;
    string name;
    string symbol;
    bytes32 salt;
    string image;
    string metadata;
    string context;
    uint256 originatingChainId;
}

struct PoolConfig {
    address hook;
    address pairedToken;
    int24 tickIfToken0IsClanker;
    int24 tickSpacing;
    bytes poolData;
}

struct LockerConfig {
    // reward info
    address[] rewardAdmins;
    address[] rewardRecipients;
    uint16[] rewardBps;
    // liquidity placement info
    int24[] tickLower;
    int24[] tickUpper;
    uint16[] positionBps;
}

struct ExtensionConfig {
    address extension;
    uint256 msgValue;
    uint16 extensionBps;
    bytes extensionData;
}

struct MevModuleConfig {
    address mevModule;
    bytes mevModuleData;
}

struct DeploymentConfig {
    TokenConfig tokenConfig;
    PoolConfig poolConfig;
    LockerConfig lockerConfig;
    MevModuleConfig mevModuleConfig;
    ExtensionConfig[] extensionConfigs;
}

// callable by anyone when the factory is not deprecated
function deployToken(DeploymentConfig memory deploymentConfig)
        external
        payable
        returns (address tokenAddress);
```

### Token Config

This is mostly unchanged from v3.1.0. The following parameters are required for token creation:

- `tokenAdmin`: The address that will manage the token's admin rights
- `name`: Token name
- `symbol`: Token symbol
- `salt`: Address salt value to allow for token address customization
- `image`: Token image
- `metadata`: Token metadata (`tokenAdmin` can update this)
- `context`: Additional token context containing information about who deployed the token (immutable)
- `originatingChainId`: Chain ID where the token's supply is deployed

Supply is set to 100 billion tokens with 18 decimals.

### Pool Config

The following parameters are required for pool creation:

- `hook`: The address of the hook to use for the pool (must be an allowlisted hook on the factory)
- `pairedToken`: The address of the paired token
- `tickIfToken0IsClanker`: The tick value to start the pool at as if the deployed token is the token0 (if the token is not the token0, the tick value will be the negative of this value)
- `tickSpacing`: The tick spacing to use for the pool
- `poolData`: Additional pool data, specific to the hook being used

### Locker Config

The lockers for v4.0.0 are more flexible than in v3.1.0 in that there is no longer a Clanker Reward Recipient and the number of reward recipients and liquidity positions are now configurable (up to 7 of each).

The following parameters are required for the liquidity locker creation:

- `rewardAdmins[]`: The addresses of the admins who will be able to modify the reward recipients that match their index
- `rewardRecipients[]`: The addresses of the recipients who will be able to receive the portion of the LP fees that match their index
- `rewardBps[]`: The portion of the LP fees to be distributed to the recipients (in basis points)
- `tickLower[]`: The lower tick value to use for a liquidity position
- `tickUpper[]`: The upper tick value to use for a liquidity position
- `positionBps[]`: The portion of the pool's token supply to be used for the liquidity position (in basis points)

The `rewardAdmin` can change the `rewardRecipient` for its position in the `rewardRecipients` array. The `rewardBps` is the portion of the LP fees to be distributed to the recipient. The total number of reward basis points must equal 10,000. The reward BPS distribution is not changeable post token deployment.

The `tickLower`, `tickUpper`, and `positionBps` arrays must be the same length and have a max length of 7. The total number of basis points in the `positionBps` array must equal 10,000. The tick ranges all must be equal to or higher than the `tickIfToken0IsClanker` value, but otherwise are not constrained beyond normal tick rules. The positions do not have to be contiguous and can overlap.

### Extension Config

The following parameters are required for the extension creation:

- `extension`: The address of the extension to use for the pool (must be an allowlisted extension on the factory)
- `msgValue`: The `msg.value` to send to the extension
- `extensionBps`: The amount of the token supply to be allocated to the extension (in basis points)
- `extensionData`: Additional data to be passed to the extension, specific to the extension being used

If the total value passed into the `deployToken` function does not match the sum of the `msgValue` of the extensions, the call will revert. The maximum amount of total extension BPS is 90%.

### MEV Module Config

The following parameters are required for the MEV module creation:

- `mevModule`: The address of the MEV module to use for the pool (must be an allowlisted MEV module on the factory)
- `mevModuleData`: Additional data to be passed to the MEV module, specific to the MEV module being used

## Token Deployment Flow

The high-level interaction between the Clanker factory and the other contracts is shown in the sequence diagram below.

![Clanker v4 Token Deployment Flow](sequence-diagrams/v4TokenDeploymentFlow.png)

## Fee Management Contracts

The `ClankerFeeLocker` and `ClankerLpLockerMultiple` contracts are used to manage the fees generated from the tokens.

### ClankerFeeLocker

The `ClankerFeeLocker` contract is the user-facing contract for managing the fees generated from the tokens. Anyone is able to trigger the `claim()` function to transfer fees from the fee locker to the user's address. This is because many users point their fees to multisigs or other contracts which cannot trigger the `claim()` function themselves.

User Facing Functions:
```solidity
// callable by anyone, transfers the available fees to the recipient
//
// note: the 'token' parameter is the token that is being claimed, not the token 
// that is generating the fees. In other places in Clanker, the 'token' parameter 
// is the token that is generating the fees.
function claim(address feeOwner, address token) external;

// view function to return the amount of `token` fees available to claim for a `feeOwner`
function availableFees(address feeOwner, address token) external view returns (uint256);
```

### ClankerLpLockerMultiple

The `ClankerLpLockerMultiple` contract is used by users to manage who is receiving the LP fees for a token's deployment liquidity. The LP is split between the list of reward recipients specified in the `LockerConfig` struct, with the split being specified in the `rewardBps` array.

The `rewardAdmin` is able to update the `rewardRecipient` for their array position (and also update themselves). The `rewardBps` array is not changeable post deployment.

User Facing Functions:
```solidity
// callable by the token's admin, updates the reward admin for the reward index
// 
// note: the 'token' parameter is the deployed token's address
function updateRewardAdmin(address token, uint256 rewardIndex, address newAdmin) external;

// callable by the token's admin, updates the reward recipient for the reward index
function updateRewardRecipient(address token, uint256 rewardIndex, address newRecipient) external;
```

## Extension Contracts

Extensions are used to add additional functionality to a token's deployment. Each extension is specified in the `DeploymentConfig` struct's `extensionConfigs` array and is executed in the order specified. They are able to request a portion of the token's supply, a `msg.value` amount of ETH, and can take actions on the token's deployed pool (such as adding liquidity or making swaps).

### ClankerVault

The `ClankerVault` extension will store a portion of the token supply for the specified `admin` to claim once lockup and vesting periods are met.

Similar to the `ClankerFeeLock`, anyone is able to trigger the `claim()` function to transfer funds from the vault to the `admin`'s address.

The `ClankerVault`'s extension data is in the form of:
```solidity
struct VaultExtensionData {
    address admin; // the address to receive the tokens, can update itself
    uint256 lockupDuration; // the duration of the lockup period
    uint256 vestingDuration; // the duration of the vesting period
}
```

The amount of tokens available to claim changes over time as the `lockupDuration` and `vestingDuration` are met. The `vestingDuration` is the amount of time after the `lockupDuration` ends that the tokens are fully vested. No minimum or maximum lockup or vesting duration is enforced.

In this version, only a single vault extension is allowed per deployment.

User Facing Functions:
```solidity
// callable by anyone, transfers the available tokens to the `admin`
function claim(address token) external;

// callable by the token's admin, updates the vault admin
function editAllocationAdmin(address token, address newAdmin) external;

// view function to return the amount of `token` available to claim
function amountAvailableToClaim(address token) external view returns (uint256);
```

### ClankerAirdrop

The `ClankerAirdrop` contract will allow a deployer to airdrop a portion of the token supply with a specified merkle root. To claim tokens, the recipient must have a leaf in the merkle root. Similar to the other claiming functions, anyone is able to trigger the `claim()` function to transfer to one of the recipients if their proof is provided.

The `ClankerAirdrop`'s extension data is in the form of:
```solidity
struct AirdropExtensionData {
    bytes32 merkleRoot; // the merkle root of the airdrop
    uint256 lockupDuration; // the duration of the lockup period
    uint256 vestingDuration; // the duration of the vesting period
}
```

Similar to the vault, an optional `lockupDuration` and `vestingDuration` is used to control the amount of tokens available to claim over time. Also similar to the vault, only a single airdrop extension is allowed per deployment.

Note: this extension does not verify onchain that the merkle root's leaves add up to the total supply of the token. It is the responsibility of the deployer to ensure that the merkle root is valid.

The airdrop expects leaves to be in abi encoded format of: `(address recipient, address token, uint256 amount)`. The OpenZeppelin `MerkleProof` library is used to verify the proof against the merkle root, with a JavaScript package available [here](https://github.com/OpenZeppelin/merkle-tree).

User Facing Functions:
```solidity
// callable by anyone, transfers the available tokens to the recipient
function claim(address token, address recipient, uint256 allocatedAmount, bytes32[] calldata proof) external;

// helper function to surface the amount available to claim for a user,
// assumes that there exists a proof for the allocated amount
function amountAvailableToClaim(address token, address recipient, uint256 allocatedAmount) external view returns (uint256);
```

### ClankerUniv4EthDevBuy

The `ClankerUniv4EthDevBuy` extension is used to perform an initial swap of the token using passed-in ETH. This provides the same functionality as the v3.1.0 `initialSwap`. Note: if the paired token for the pool is not WETH, only paired tokens which have a WETH<>PairedToken Uniswap v4 pool will be able to use this extension.

In this version, multiple devBuy extensions are allowed per deployment.

The `ClankerUniv4EthDevBuy`'s extension data is in the form of:
```solidity
struct Univ4EthDevBuyExtensionData {
    // pool key to swap from W/ETH to paired token if the paired token is not WETH
    PoolKey pairedTokenPoolKey;
    // minimum amount of token to receive from the W/ETH -> paired token swap
    uint128 pairedTokenAmountOutMinimum;
    // recipient of the tokens
    address recipient;
}
```

## Clanker v4 Hooks

The `ClankerHook` contract is the base contract that our other hooks inherit from. It is `abstract` and hooks built on top of it have the ability to change the LP fee taken on a swap.

The `ClankerHook` contract itself facilitates:
- Automatic fee collection of the initial LP position's fees (fees are collected by the `ClankerHook` contract and routed to the `ClankerFeeLocker` contract for users to claim)
- Collection of a DEX-level protocol fee, always in the paired token
- Triggering of a pool's `MevModule` until it is disabled or expired

Note: both of the fee collection mechanisms lag by a swap, so the fees collected in a swap are the fees from the previous swap. This is because a swap's fees are only sent into the `PoolManager` contract after the swap has completed.

v4.0.0's initial release introduces two different fee hook types: `ClankerHookStaticFee` and `ClankerHookDynamicFee`. For both hooks, the protocol's DEX fee is always taken in the paired token and will always be 20% of the active LP fee.

The maximum LP fee is 80% of a swap. It is NOT recommended to set the fee this high if it is desired for the token to work with every router; fees that high should be reserved for esoteric use cases only.

Clanker's hooks allow for deployment of pools not created by the factory via an 'open' version of the initialization pathway. Pools created via this method do not have automatic LP fee claiming or mev module operations. Note: this function will revert if called with the `clanker` address set to `WETH`. We only collect the protocol's fee on the paired token and would prefer it to be `WETH` when possible.

```solidity
// callable by anyone
function initializePoolOpen(
        address clanker,
        address pairedToken,
        int24 tickIfToken0IsClanker,
        int24 tickSpacing,
        bytes calldata poolData
    ) public returns (PoolKey memory)
```

### ClankerHookStaticFee

The `ClankerHookStaticFee` hook is used to set a static fee for both the deployed token and the paired token.

The static fee's hook data is in the form of:
```solidity
struct PoolStaticConfigVars {
    uint24 clankerFee; // the fee taken on the deployed token when it's the input token
    uint24 pairedFee; // the fee taken on the paired token when it's the input token
}
```

### ClankerHookDynamicFee

The `ClankerHookDynamicFee` hook is used to set a dynamic fee for both the deployed token and the paired token. The math governing the dynamic fee took inspiration from Meteora's Dynamic Liquidity Market Maker's [fee model](https://docs.meteora.ag/product-overview/meteora-liquidity-pools/dlmm-overview/dynamic-fees).

The goal of the dynamic fee is to charge higher fees when more volatility is present on the pool. We measure the volatility by simulating the price impact of a swap (measured in the difference between the `beforeTick` and `afterTick`) and applying a fee based on the magnitude. The resulting fee we apply is a mixture of the tick difference, recent swaps on the pool, and safeguards to prevent bot manipulation.

The dynamic fee is calculated by the following equation:

```solidity
uint24 dynamicFee = MIN((baseFee + (feeControlNumerator * (volatilityAccumulator ** 2)) / FEE_CONTROL_DENOMINATOR), maxLpFee);
```

The `volatilityAccumulator` is a value that is updated on each swap and is calculated by the process described below.

#### Volatility Accumulator

The volatility accumulator is our way of measuring the amount of volatility on the pool. It is a value that is updated on each swap and is affected by recent swaps on the pool under different time frames. We use two lagging tick values, two time periods, and a decayed version of a previous volatility accumulator to calculate a swap's volatility accumulator. 

The equation for the volatility accumulator is:

```solidity
volatilityAccumulator = |afterTick - referenceTick| + prevVR;
```

The values of `referenceTick` and `prevVR` are updated based on snapshots of pool activity captured by the lagging ticks and time periods.

The two lagging tick values are:
- `referenceTick`: When the `referenceTickFilterPeriod` has passed or if a `resetPeriod` has been triggered with a failing reset condition, this tick value is updated to the `beforeTick` of a swap. This tick value is kept the same until hitting one of these update conditions.
- `resetTick`: This tick is updated to the `beforeTick` of a swap when the `referenceTick` is updated, and is additionally updated when the `resetPeriod` has passed and a swap's `beforeTick` is at least `resetTickFilter` ticks away from the `resetTick` (a passing reset condition).

The two time periods and their uses are:
- `referenceTickFilterPeriod`: If the previous swap was less than `referenceTickFilterPeriod` seconds away, we use the recorded `referenceTick` for the dynamic fee calculation instead of the `beforeTick` of the current swap. This prevents bots from avoiding the dynamic fee by cutting their swap into smaller swaps.
- `resetPeriod`: If a `referenceTick` has not been updated for over `resetPeriod` seconds, we want to ensure that there is true volatility happening on the pool. The goal is to prevent bots from keeping the pool's fee high by sending in micro swaps to avoid updating the `referenceTick`. If the `resetPeriod` has passed and the `beforeTick`'s difference from the recorded `resetTick` is smaller than the `resetTickFilter` (a failing reset condition), we update both the `referenceTick` and `resetTick` to the `beforeTick` of the current swap to clear out the stored volatility. If the `resetPeriod` has passed and the `beforeTick`'s difference from the recorded `resetTick` is larger than the `resetTickFilter` (a passing reset condition), we only update the `resetTick` to the `beforeTick` of the current swap and keep the `referenceTick` the same.

Additionally, the two time periods are used to determine the `prevVR` value. The `prevVR` is used to smooth volatility over a period of time and is set to a value when the `referenceTickFilterPeriod` has passed but the `resetPeriod` has not. The value is set to the last swap's volatility accumulator multiplied by the `decayFilterBps` value. The `prevVR` is set to zero when the `resetPeriod` passes with a failed condition or if a `resetPeriod` amount of time has passed without a swap. The same `prevVR` is used as long as the `referenceTick` has not been updated.

Note: there is a slight drift between the simulated `afterTick` and the resulting `afterTick`. This is because the applied fee for the simulated swap is an estimate either reusing the previous swap fee or just the `prevVR` under some reset conditions. We deemed that this drift is acceptable for our intended outcomes.

#### User Controlled Configurable Variables

We provide a recommended set of configuration values for the dynamic fee hook, but users are able to configure their own:

```solidity
struct PoolDynamicConfigVars {
    uint24 baseFee; // the minimum LP fee to be taken on a swap
    uint24 maxLpFee; // the maximum LP fee to be taken on a swap
    uint256 referenceTickFilterPeriod;
    uint256 resetPeriod;
    int24 resetTickFilter;
    uint256 feeControlNumerator; // the denominator is set to 1_000_000
    uint24 decayFilterBps;
}
```

We have a spreadsheet that replicates this process and can provide it upon request.

## MEV Modules

Clanker v4.0.0 introduces the ability to modify a pool's behavior at the time of deployment through different MEV modules. Our long-term goal for these modules is to capture more value from the sniping activity on our pools, but initially they are being utilized to fix negative externalities enabled by our older contracts' usage of Uniswap v3.

When Clanker was initially deployed on v3, searchers caused a spike in the gas price of the network by attempting to search for deployments in the same block (documented [here](https://x.com/0xdoug/status/1861662510023123030)). The solution to this problem was to add a delay between when a pool was deployed and when it was tradeable. Our initial MEV module, `ClankerMevModuleDelay`, is used to add a delay between when a pool is deployed and when it is enabled for searching.

We plan to add more MEV modules in the future to allow for more complex behaviors. See our [auction proposal](./AuctionProposal.md).

Functionally, MEV modules are specified on pool deployment and are enabled after a token has finished deploying. This happens after extensions have finished executing to allow extensions to either make swaps or add liquidity to the pool. MEV Modules are active until they disable themselves or expire after 2 minutes. We have this expiry period as a safeguard in case the pool's underlying sequencer environment changes and the MEV module breaks. Additionally, liquidity adds are restricted during the MEV module's operation, as we want to reserve the ability to use the `donate()` function to send payments to only the beneficiaries of the original LP position.

## ClankerToken

The tokens deployed by the Clanker factory are ERC20s with ERC20Permit, ERC20Votes and ERC20Burnable capabilities.

Tokens are deployed with an 'admin' key which is able to verify the contract, update the image, and update the metadata.

User Facing Functions:
```solidity
// only callable by token's admin
function updateImage(string memory image_) external;

// only callable by token's admin
function updateMetadata(string memory metadata_) external;

// only callable by token's admin
function verify() external;
```

## Administrative Permissions

Clanker as a whole aims to be permissionless where possible and is committed to decentralization. All of our contracts currently either have no owner or are owned by Clanker's team multisig. Any 'admin' role can be assumed to be controlled by either Clanker multisigs or EOAs. None of the contracts are upgradeable.

### Clanker.sol
Role: `Owner`

Permissions:
- Add/remove admins via `setAdmin()`
- Initialize the contract with `locker` and `teamFeeRecipient` addresses (can only be done once)
- Pause supplied token deployments with `setDeprecated()`
- Change the `teamFeeRecipient` via `setTeamFeeRecipient()`
- All of admin's capabilities

Role: `Admin`

Permissions:
- Claim protocol fees to `teamFeeRecipient` via `claimTeamFees()`
- Enable/disable hooks via `setHook()`
- Enable/disable extensions via `setExtension()`
- Enable/disable mev modules via `setMevModule()`

### ClankerLpLockerMultiple.sol
Role: `Owner`

Permissions:
- Withdraw ETH sent to the contract by accident via `withdrawEth()`
- Withdraw ERC20s sent to the contract by accident via `withdrawERC20()`

### ClankerFeeLocker.sol
Role: `Owner`

Permissions:
- Add additional depositors via `addDepositor()`

### No Administrative Permissions

Clanker has no administrative abilities on the following contracts:
- ClankerHook.sol
- ClankerStaticHook.sol
- ClankerDynamicHook.sol
- ClankerAirdrop.sol
- ClankerVault.sol
- ClankerUniv4EthDevBuy.sol
- ClankerMevBlockDelay.sol
- ClankerToken.sol

## SuperChain Compatibility

Clanker is `SuperChainERC20` compatible such that if a token is deployed on one chain, it can be re-deployed on other compatible super-chains with the same token address. The `originatingChainId` parameter is used to determine if the initial supply should be minted. If the `originatingChainId` is not the current chain, zero supply will be minted and users are expected to utilize the super-chain's bridge to migrate their tokens. Tokens can only be minted with supply on the originating chain, and cannot be minted with zero supply on the originating chain.

We use Foundry's `CREATE2` deployment support to ensure that our factory contract (Clanker) can have the same address on different chains. This is needed to have the same resulting token addresses on the different chains.

Super-chain documentation can be found [here](https://docs.optimism.io/stack/interop/superchain-erc20).

Note: for a token to be able to be bridged between super-chains, the superchains must be in the same superchain cluster. This is subject to user error if the Clanker contract is not properly initialized on the target chain, and if the target chain is not part of the same superchain cluster.

The function `deployTokenZeroSupply()` can be used to deploy a token with zero supply. This is useful for tokens that are bridged to a super-chain:

```solidity
// Use the same tokenConfig as that was used to deploy the token on the originating chain
function deployTokenZeroSupply(TokenConfig memory tokenConfig) external returns (address tokenAddress) {...}
```